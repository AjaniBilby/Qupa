program ::= w* ( stmt_top w* )*
	stmt_top ::= library | function

#=============================
#  Helper patterns
#=============================
	w ::= " " | "\t" | nl
	nl ::= "\r\n" | "\n"

	digit ::= "0" | "1" | "2" | "3" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
		digit_nz ::= "1" | "2" | "3" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
	letter ::= letter_upper | letter_lower
		letter_lower ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "t" | "s" | "u" | "v" | "w" | "x" | "y" | "z"
		letter_upper ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "T" | "S" | "U" | "V" | "W" | "X" | "Y" | "Z"


#=============================
#  Constants
#=============================
comment ::= comment_single | comment_multi
	comment_single ::= "//" !( nl )* nl
	comment_multi  ::= "/*" ( "\\*" | !( "*/" )* )* "*/"


#=============================
#  Constants
#=============================
	constant ::= boolean | string | hexidecimal | octal | binary | float | integer

	string ::= string_unicode | string_text | string_multiline
		string_unicode   ::= "\"" ( "\\\"" | !( "\"" )+ )* "\""
		string_text      ::= "'" ( "\\'"  | !( "'"  )+ )* "'"
		string_multiline ::= "`" ( "\\`"  | !( "`"  )+ )* "`"

	hexidecimal ::= "0x" hex_char+
		hex_char ::= digit | "a" | "b" | "c" | "d" | "e" | "f" | "A" | "B" | "C" | "D" | "E" | "F"

	octal ::= "0o" octal_char+
		octal_char ::= "0" | "1" | "2" | "3"

	binary ::= "0b" ( "0" | "1" )+

	boolean ::= "true" | "false"

	integer ::= digit+
	float   ::= integer "." integer ( "e" "-"? integer )?


#=============================
#  Library Management
#=============================
	library ::= import | expose

	import ::= import_as | import_direct
		import_as ::= "import" w* string w* ";"
		import_direct ::= "import" w* string w+ "as" w+ name w* ";"

	expose ::= "expose" w+ ( "*" | name ) w* ";"


#=============================
#  Variables
#=============================
	name ::= letter ( letter | digit | "_" )*
	variable ::= name accessor?
		accessor ::= ( accessor_arrow | accessor_dot | accessor_dynamic ) accessor?
			accessor_arrow ::= "->" name
			accessor_dot   ::= "." name
			accessor_dynamic ::= "[" w* expr w* "]"

	data_type ::= pointer | variable
		pointer ::= "@" variable

	declare ::= data_type w+ name w* ";"


#=============================
#  Function
#=============================
function ::= data_type w+ name w* function_template? w* function_arguments w* function_modifier? w* function_body
	function_template ::= "[" w* ( func_template_arg w* )? ( "," w* func_template_arg w* )* "]"
		func_template_arg ::= "^" data_type w+ name
	function_modifier ::= ":" w* ( func_mod w* )+
		func_mod ::= "inline" | "async"
	function_arguments ::= "(" w* ( func_arg w* )? ( "," w* func_arg w* )* ")"
		func_arg ::= "^"? data_type w+ name ( w* "=" w* constant )?
	function_body ::= "{" w* ( func_stmt w* )* "}"
	func_stmt ::= return | declare

	return ::= "return" w+ expr w* ";"

#=============================
#  Expression
#=============================
expr ::= expr_opperand
	expr_opperand ::= variable | constant